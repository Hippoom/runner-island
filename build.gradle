buildscript {
    ext {
        spockVersion = "1.1-groovy-2.4-rc-3"
    }

    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
    }
}

plugins {
    id 'org.springframework.boot' version '1.5.2.RELEASE'
    id "io.spring.dependency-management" version "1.0.1.RELEASE"

}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    mavenLocal()
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    mavenCentral()
}

dependencyManagement {
    dependencies {
        dependency "org.projectlombok:lombok:1.16.10"
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok"
    testCompileOnly "org.projectlombok:lombok"

    compile("org.springframework.boot:spring-boot-starter-web")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile group: 'io.rest-assured', name: 'spring-mock-mvc', version: '3.0.2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.spockframework', name: 'spock-spring', version: "${spockVersion}"
    testCompile group: 'org.spockframework', name: 'spock-core', version: "${spockVersion}"
    // spock-spring introduces incompatible core dependency
    testCompile 'cglib:cglib-nodep:3.2.2' // for stubbing of classes in spock
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.9'
    testCompile('com.athaydes:spock-reports:1.2.13') {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }

}

test {
    exclude '**/specs/*'
    exclude '**/*LearningTest*'
    exclude '**/*Console*'

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

test.finalizedBy jacocoTestReport

task featureTest(type: Test) {
    // this task will be executed in isolation since all artifacts have been built and deployed
    dependsOn = []

    include '**/specs/*'

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    outputs.upToDateWhen { false }//force to always rerun

    reports {
        html {
            destination = "$buildDir/reports/feature"
        }
    }
}

idea {
    module {
        scopes.COMPILE.plus += [configurations.compileOnly]
    }
}
